domain etat : bool [3] ;
domain action : bool [2] ;

$--- Ensemble des transitions de l'automate
#Trans [qs:etat, a:action, qd:etat] :=
 ({qs=0} & {a=0} & {qd=1}) | ({qs=0} & {a=0} & {qd=1}) |
 ({qs=1} & {a=1} & {qd=4}) | ({qs=2} & {a=1} & {qd=3}) |
 ({qs=3} & {a=2} & {qd=5}) | ({qs=4} & {a=2} & {qd=5}) |
 ({qs=5} & {a=3} & {qd=6}) | ({qs=5} & {a=3} & {qd=7}) ;

#accessible [qs:etat] := true ;

$--- Calcul de la plus grande auto-bisimulation
display #bisimul [q1:etat, q2:etat] -=
  #accessible [q1] & #accessible [q2] &
  ! q1d:etat, a:action (#Trans [q1, a, q1d] ->
                ?q2d:etat (#Trans [q2, a, q2d] & #bisimul [q1d, q2d])
               ) &
  ! q2d:etat, a:action (#Trans [q2, a, q2d] ->
                ?q1d:etat (#Trans [q1, a, q1d] & #bisimul [q1d, q2d])
               ) ;

$--- Calcul de l'ensemble reduit des etats
display #Qreduit [q1:etat] := !q2:etat (#bisimul [q1, q2] -> {q1 <= q2}) ;

$--- Calcul de l'ensemble reduit des transitions
display #TransReduit [qs:etat, a:action, qd:etat] := 
     #Qreduit [qs] & #Qreduit [qd] &
     ?qss:etat, qdd:etat (#Trans [qss, a, qdd] & #bisimul [qs,qss] & #bisimul [qd,qdd]) ;
end
