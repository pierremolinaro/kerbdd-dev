domain N : bool [3] ;

$ --- Ensemble des (x, y) tels que y = x + 1
#diff1 [y:bool, x:bool] := ?u:bool ({x<u} & {u<=y} & !v:bool (({x<v} & {v<=y}) -> {u=v})) ; 

#diff1bis [x:N, y:N] := ?u:N ({x<u} & {u<=y} & !v:N (({x<v} & {v<=y}) -> {u=v})) ; 

$ --- Ensemble des (x, y, z) tels que z = x + y
#somme[x:N, y:N, z:N] += ( {x=0} & {y=z} ) | ({x=1} & #diff1bis [y,z]) | ?a:N , b:N, c:N (#somme[a,c,x] & #somme [y,c,b] & #somme [a,b,z] ); 

$ --- Ensemble des (res, x, y) tels que res = max(0, x - y) 
#max0moins[res:N, x:N, y:N] := (?z:N (#somme[z,y,x] & {res=z} )) | (~?z:N (#somme[z,y,x] ) & {res=0} ) ; 


$ --- inf de deux valeurs z = inf2(a1:N , a2:N) 
#inf2 [a1:N, a2 :N, z:N] := if {a1>a2} then {z=a2} else {z=a1} end ; 

$ --- inf de trois valeurs z = inf3(a1:N, a2:N, a3:N) 
#inf3 [a1:N, a2:N, a3:N, z:N] := ?u:N (#inf2 [a1, a2, u] & #inf2 [a3, u, z] ) ; 

display #placeTransition [p4: bool, p3: bool, p2: bool, p1: bool, tr: bool] := 
 (p1 & ~p2 & ~p3 & ~p4 & {tr= 0}) | (~p1 & ~p2 & ~p3 & p4 & {tr= 1}) ;

display #transitionPlace [tr: bool, p4: bool, p3: bool, p2: bool, p1: bool] := 
 ({tr= 0} & ~p1 & p2 & ~p3 & ~p4) | ({tr= 1} & ~p1 & ~p2 & p3 & ~p4) ;

display #M0 [p4s:bool, p3s:bool, p2s:bool, p1s:bool] := {p1s = 1} & {p2s = 0} & {p3s = 0} & {p4s = 1} ; 

#graphe [tr:bool,
         p4s: bool, p3s: bool, p2s: bool, p1s: bool, p4d: bool, p3d: bool, p2d: bool, p1d: bool] := 
 ? p4: bool, p4dd: bool, p4i : bool,
   p3: bool, p3dd: bool, p3i : bool,
   p2: bool, p2dd: bool, p2i : bool,
   p1: bool, p1dd: bool, p1i : bool 
 (#placeTransition [p4, p3, p2, p1, tr] & #transitionPlace [tr, p4dd, p3dd, p2dd, p1dd]
   &
  (if p4 then #diff1 [p4s, p4i] else {p4s = p4i} end)
   &
  (if p3 then #diff1 [p3s , p3i] else {p3s = p3i} end)
   &
  (if p2 then #diff1 [p2s, p2i] else {p2s = p2i} end)
   &
  (if p1 then #diff1 [p1s , p1i] else {p1s = p1i} end)
   &
  (if p4dd then #diff1 [p4d, p4i] else {p4d = p4i} end)
   & 
  (if p3dd then #diff1 [p3d, p3i] else {p3d = p3i} end)
   &
  (if p2dd then #diff1 [p2d , p2i] else {p2d = p2i} end)
   &
  (if p1dd then #diff1 [p1d, p1i] else {p1d = p1i} end)
 )
;

display #marques [p4s: bool, p3s: bool, p2s: bool, p1s: bool] += 
 #M0 [p4s, p3s, p2s, p1s] |
 ?tr:bool, p4:bool, p3:bool, p2:bool, p1:bool
 (#marques [p4, p3, p2, p1] & #graphe [tr, p4, p3, p2, p1, p4s, p3s, p2s, p1s])
; 

display #grapheMarquage [tr: bool, p4s: bool, p3s: bool, p2s: bool, p1s: bool, p4d: bool, p3d: bool, p2d: bool, p1d: bool] := 
 #marques [p4s, p3s, p2s, p1s] & #graphe [tr, p4s, p3s, p2s, p1s, p4d, p3d, p2d, p1d]
; 

display #temps [ tr:bool, tmin:N, tmax:N ] :=
 ({tr = 0 } & {tmin = 3} & {tmax = 5}) | ({tr = 1} & {tmin = 2} & {tmax = 4})
;

display #domaine0 [s4:bool, s3:bool, s2:bool, s1:bool, tr1max:N, tr0max:N, tr1min:N, tr0min:N] := 
 #M0 [s4, s3, s2, s1] &
 (! trans:bool
  (? trmin:N, trmax:N
   (#temps [trans, trmin, trmax]
       &
    (
     (
      (? des4:bool, des3:bool, des2:bool, des1:bool
       (#grapheMarquage [trans, s4, s3, s2, s1, des4, des3, des2, des1])
      )
         &
      ({trans = 0} -> {tr0min = trmin} & {tr0max = trmax})
         &
      ({trans = 1} -> {tr1min = trmin} & {tr1max = trmax})
     )
$       |
$     (
$      (~? des4:bool, des3:bool, des2:bool, des1:bool
$       (#grapheMarquage [trans, s4, s3, s2, s1, des4, des3, des2, des1])
$      )
$         &
$      ({trans = 0} -> {tr0min = 6} & {tr0max = 6})
$         &
$      ({trans = 1} -> {tr1min = 6} & {tr1max = 6})
$     )
    )
   )
  )
 )
;

display #firstdomaine [tr:bool,
                       s4:bool, s3:bool, s2:bool, s1:bool,
                       d4:bool, d3:bool, d2:bool, d1:bool,
                       tr1sma:N, tr0sma:N, tr1smi:N, tr0smi:N, tr1dma:N, tr0dma:N, tr1dmi:N, tr0dmi:N] := 
 #domaine0 [s4, s3, s2, s1, tr1sma, tr0sma, tr1smi, tr0smi]
    & 
 #grapheMarquage [tr, s4, s3, s2, s1, d4, d3, d2, d1]
    &
 ?trmaximum:N, trminimum:N
 (#inf2 [ tr1sma , tr0sma ,trmaximum ]
  &
 ({tr= 0} -> {tr0smi <= trmaximum} & {trminimum =tr0smi})
  &
 ({tr= 1} -> {tr1smi <= trmaximum} & {trminimum =tr1smi})
  &
 ! trans:bool
  (
   (
    (? des4:bool, des3:bool, des2:bool, des1:bool
     (#grapheMarquage [trans, d4, d3, d2, d1, des4, des3, des2, des1])
    )
     &
    (? trmin :N, trmax:N (
     #temps [trans, trmin, trmax]
      &
     (({trans = 0} & {tr0smi < 6}) -> (#max0moins [tr0dmi, tr0smi, trmaximum] & #max0moins[tr0dma, tr0sma, trminimum]))
      &
     (({trans = 0} & {tr0smi = 6}) -> {tr0dmi = trmin} & {tr0dma = trmax})
      &
     (({trans = 1} & {tr1smi < 6}) -> (#max0moins[tr1dmi, tr1smi, trmaximum] & #max0moins[tr1dma, tr1sma, trminimum]))
      &
     (({trans = 1} & {tr1smi = 6}) -> {tr1dmi = trmin} & {tr1dma = trmax})) 
    )
     |
    (~? des4:bool, des3:bool, des2:bool, des1:bool (#grapheMarquage [trans, d4, d3, d2, d1, des4, des3, des2, des1])) 
     &
    ({trans = 0} -> {tr0dmi = 6} & {tr0dma = 6}) 
     &
    ({trans= 1} -> { tr1dmi = 6} & {tr1dma = 6})
   )
  )
 )
;

display #grapheClasse [tr:bool,
                       s4:bool, s3:bool, s2:bool, s1:bool,
                       d4:bool, d3:bool, d2:bool, d1:bool,
                       tr1sma:N, tr0sma:N, tr1smi:N, tr0smi:N, tr1dma:N, tr0dma:N, tr1dmi:N, tr0dmi:N] += 
 #firstdomaine [tr, s4, s3, s2, s1, d4, d3, d2, d1, tr1sma, tr0sma, tr1smi, tr0smi, tr1dma, tr0dma, tr1dmi, tr0dmi]
  |
 #grapheMarquage [tr, s4, s3, s2, s1, d4, d3, d2, d1]
  &
 (?trmaximum:N, trminimum:N
  (#inf2 [tr1sma, tr0sma, trmaximum]
   &
   ({tr = 0} -> {tr0smi <= trmaximum} & {trminimum = tr0smi})
    &
   ({tr = 1} -> {tr1smi <= trmaximum} & {trminimum = tr1smi})
    &
   ! trans:bool
    (
     (
      (? des4:bool, des3:bool, des2:bool, des1:bool (#grapheMarquage [trans, d4, d3, d2, d1, des4, des3, des2, des1]))
       &
      (? trmin:N, trmax:N
       (#temps [trans, trmin, trmax]
        &
       (({trans = 0} & {tr0smi < 6}) -> (#max0moins[tr0dmi, tr0smi, trmaximum] & #max0moins [tr0dma, tr0sma, trminimum]))
        &
       (({trans = 0} & {tr0smi = 6}) -> {tr0dmi = trmin} & {tr0dma = trmax})
        &
       (({trans = 1} & {tr1smi < 6}) -> (#max0moins [tr1dmi, tr1smi, trmaximum] & #max0moins [tr1dma, tr1sma, trminimum]))
        &
       (({trans= 1} & {tr1smi = 6}) -> {tr1dmi = trmin} & {tr1dma = trmax}) 
      )
     )
      |
     (~? des4:bool, des3:bool, des2:bool, des1:bool (#grapheMarquage [trans, d4, d3, d2, d1, des4, des3, des2, des1])) 
      &
     ({trans = 0} -> {tr0dmi = 6} & {tr0dma = 6}) 
      &
     ({trans = 1} -> {tr1dmi = 6} & {tr1dma = 6})
    )
   )
  )
 )
  &
 (? trplus:bool, dd4:bool, dd3:bool, dd2:bool, dd1:bool, tr1ddma:N, tr0ddma:N, tr1ddmi:N, tr0ddmi:N
  (#grapheClasse [trplus, dd4, dd3, dd2, dd1, s4, s3, s2, s1, tr1ddma, tr0ddma, tr1ddmi, tr0ddmi, tr1sma, tr0sma, tr1smi, tr0smi])
 )
; 


display #grapheClasseReduit [tr:bool, s4:bool, s3:bool, s2:bool, s1:bool, d4:bool, d3:bool, d2:bool, d1:bool] :=
 ? tr1sma:N, tr0sma:N, tr1smi:N, tr0smi:N, tr1dma:N, tr0dma:N, tr1dmi:N, tr0dmi:N
 (#grapheClasse [tr, s4, s3, s2, s1, d4, d3, d2, d1, tr1sma, tr0sma, tr1smi, tr0smi, tr1dma, tr0dma, tr1dmi, tr0dmi])
; 


end