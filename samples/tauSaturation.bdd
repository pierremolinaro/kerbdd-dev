$--- Ensemble des transitions de l'automate
#Trans [qs:3, a:2, qd:3] :=
 ({qs=0} & {a=0} & {qd=1}) | ({qs=1} & {a=1} & {qd=2}) |
 ({qs=2} & {a=0} & {qd=3}) | ({qs=3} & {a=0} & {qd=4}) |
 ({qs=4} & {a=0} & {qd=2}) | ({qs=4} & {a=2} & {qd=5}) |
 ({qs=5} & {a=3} & {qd=6}) | ({qs=6} & {a=0} & {qd=7}) ;

#accessible [qs:3] := true ;

$--- tau-saturation de l'automate
display #transSat [qs:3, a:2, qd:3] += #Trans [qs, a, qd] |
   ({qs=qd} & {a=0}) |
   ?q:3, tau:2 (#transSat [qs, a, q] & #transSat [q, tau, qd] & {tau = 0}) |
   ?q:3, tau:2 (#transSat [qs, tau, q] & #transSat [q, a, qd] & {tau = 0}) ;

$--- Calcul de la plus grande auto-bisimulation
display #bisimul [q1:3, q2:3] -=
  #accessible [q1] & #accessible [q2] &
  ! q1d:3, a:2 (#transSat [q1, a, q1d] ->
                ?q2d:3 (#transSat [q2, a, q2d] & #bisimul [q1d, q2d])
               ) &
  ! q2d:3, a:2 (#Trans [q2, a, q2d] ->
                ?q1d:3 (#transSat [q1, a, q1d] & #bisimul [q1d, q2d])
               ) ;

$--- Calcul de l'ensemble rŽduit des Žtats
display #Qreduit [q1:3] := !q2:3 (#bisimul [q1, q2] -> {q1 <= q2}) ;

$--- Calcul de l'ensemble rŽduit des transitions (conserve les boucles tau)
display #TransReduit [qs:3, a:2, qd:3] :=
   #transSat [qs, a, qd] & #Qreduit [qs] & #Qreduit [qd] ;

$--- Calcul de l'ensemble rŽduit des transitions (sans les boucles tau)
display #TransReduit2 [qs:3, a:2, qd:3] :=
   #transSat [qs, a, qd] & #Qreduit [qs] & #Qreduit [qd] & ~({qs=qd} & {a=0}) ;

$--- Calcul de l'ensemble rŽduit des transitions (en conservant les boucles tau
$ de l'automate d'origine)
display #boucleTau [qs:3] -= $ Ensemble des Žtats appartenant ˆ une boucle tau
  #accessible [qs] &
  ?qd:3, a:2 ((#Trans [qs, a, qd] & {a=0}) & #boucleTau [qd]) ;

display #TransReduit3 [qs:3, a:2, qd:3] :=
   #transSat [qs, a, qd] & #Qreduit [qs] & #Qreduit [qd] & 
     (({qs=qd} & {a=0}) -> #boucleTau [qs]) ;

end
