$--------------------------------------------------------------*
$                                                              *
$    Test des precedence d'operateurs sur la grammaire         *
$                                                              *
$--------------------------------------------------------------*

$------- Construction des relations de precedence --------------------
#PREC_EGAL [r:NTT, s:NTT] := ?ntg:NTT #PRODUCTION [ntg, r, s] ;

#PREC_INF [r:NTT, s:NTT] :=
                     ?u:NTT, v:NTT (#PRODUCTION [u, r, v] & #PREMIERS [v, s]) ;

#PREC_SUP [r:NTT, s:NTT] :=
             ?u:NTT, v:NTT, w:NTT (#PRODUCTION [u, v, w] & #FT_LAST [v, r]
                                                         & #PREMIERS_REFLEXIFS [w, s]) ;

$--- Test pour savoir si la grammaire est une grammaire de precedence
$ Chaque ensemble CONFLIT doit etre vide

display #CONFLIT_PG_1 [r:NTT, s:NTT] := #PREC_EGAL [r, s] & #PREC_INF [r, s] ;

display #CONFLIT_PG_2 [r:NTT, s:NTT] := #PREC_EGAL [r, s] & #PREC_SUP [r, s] ;

display #CONFLIT_PG_3 [r:NTT, s:NTT] := #PREC_SUP [r, s] & #PREC_INF [r, s] ;

display #CONFLIT_PG_4 [r:NTT, s:NTT] := 
     ?u:NTT, v:NTT ({r < s} & #PRODUCTION [r, u, v] & #PRODUCTION [s, u, v]) ;

$--- Test pour connaitre la recursivite a gauche --------------
#RECURSIVITE_A_GAUCHE [nt:NTT] := #PREMIERS [nt, nt] ;

end

